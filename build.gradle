import org.gradle.api.internal.classpath.ModuleRegistry
import org.gradle.api.internal.project.ProjectInternal

buildscript {
  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:1.2.0"
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.16.0'
  }
}

apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'java-gradle-plugin'
apply plugin: "com.vanniktech.maven.publish"
apply plugin: 'com.gradle.plugin-publish'

repositories {
  mavenCentral()
  google()
  jcenter()
}

dependencies {
  api gradleApi()
  api localGroovy()

  compileOnly 'com.android.tools.build:gradle:8.0.0'

  testImplementation 'com.android.tools.build:gradle:8.0.0'
  testImplementation 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.21'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
  testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-3.0', { exclude module: "groovy-all" } // Use localGroovy()

  // https://github.com/gradle/gradle/issues/16774#issuecomment-893493869
  def toolingApiBuildersJar = (project as ProjectInternal).services.get(ModuleRegistry.class)
    .getModule("gradle-tooling-api-builders")
    .classpath
    .asFiles
    .first()
  testRuntimeOnly(files(toolingApiBuildersJar))
}

sourceCompatibility = JavaVersion.VERSION_17

gradlePlugin {
  website = POM_URL
  vcsUrl = POM_SCM_URL

  plugins {
    androidJUnitJacocoPlugin {
      id = POM_ARTIFACT_ID
      implementationClass = POM_IMPLEMENTATION_CLASS
      displayName = POM_NAME
      description = POM_DESCRIPTION
      tags.set(['gradle', 'android', 'jacoco', 'app module', 'library module', 'junit', 'unit', 'testing', 'coverage'])
    }
  }
}

tasks.withType(Test).configureEach {
  testLogging {
    testLogging.exceptionFormat = 'full'
  }
}

wrapper {
  gradleVersion = '8.0'
  distributionType = Wrapper.DistributionType.ALL
}
